plugins {
    id 'maven-publish'
    id "org.jetbrains.kotlin.jvm" version "1.6.10"
    id "org.jetbrains.kotlin.kapt" version "1.6.10"
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

allprojects {
    group 'gg.scala.cgs'
    version "1.0.4"

    apply plugin: "maven-publish"
    apply plugin: "com.github.johnrengelman.shadow"
    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: "org.jetbrains.kotlin.kapt"

    repositories {
        mavenLocal()
        mavenCentral()

        maven {
            url "https://jitpack.io"
        }

        maven {
            url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

            content {
                includeGroup 'org.bukkit'
                includeGroup 'org.spigotmc'
            }
        }

        maven {
            url "https://repo.aikar.co/content/groups/aikar/"
        }
    }

    dependencies {
        compileOnly "org.jetbrains.kotlin:kotlin-stdlib"

        implementation 'com.github.growlyx:flavor:0.1.2'
        implementation 'com.github.devrawr:honey:435a80e143'

        compileOnly "org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT"
        compileOnly "me.lucko:helper:5.6.9"

        implementation 'net.kyori:adventure-api:4.9.3'
        implementation 'net.kyori:adventure-text-minimessage:4.9.3'
        implementation "net.kyori:adventure-platform-bukkit:4.0.1"

        implementation "co.aikar:acf-bukkit:0.5.1-SNAPSHOT"

        kapt "me.lucko:helper:5.6.9"
    }

    compileKotlin {
        kotlinOptions.javaParameters = true
        kotlinOptions.jvmTarget = "1.8"
    }

    shadowJar {
        archiveClassifier.set("")

        exclude "**/*.kotlin_metadata"
        exclude "**/*.kotlin_builtins"
        exclude "META-INF/"

        archiveFileName = "cgs-${project.name}.jar"
    }

    publishing {
        publications {
            shadow(MavenPublication) { publication ->
                project.shadow.component(publication)
            }
        }
    }

    tasks.build.dependsOn(shadowJar)
}